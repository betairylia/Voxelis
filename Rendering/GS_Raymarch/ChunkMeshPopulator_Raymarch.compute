// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#pragma kernel GetVertCount
#include "../../Data/Block.cginc"

// NOTICE THAT GChunks are in ZYX format

struct Vertex
{
    float3 position;
    float3 normal;
    float2 uv;
    uint block_vert_meta;
    Block data;
};

int3 flatFactor;
int fsBufLenX, fsBufLenY; // xLen, yLen

StructuredBuffer<Block> chunkData;
RWStructuredBuffer<unsigned int> fsPointerBuffer;
RWStructuredBuffer<unsigned int> indirectBuffer;
RWStructuredBuffer<Vertex> vertexBuffer;

// Could be used to handle non-4-Byte stride Block buffers.
inline Block ReadPackedBlockBuffer(StructuredBuffer<Block> buf, int idx)
{
    return buf[idx];
}

inline void SetVert(uint3 id : SV_DispatchThreadID, int currPos, out int currIdx, out bool3 facePos, out bool3 faceNeg)
{
    // Count vertices number
    int vCount = 0;
    facePos = bool3(false, false, false);
    faceNeg = bool3(false, false, false);

    if (id.x <= 0)
    {
        faceNeg.x = true;
        vCount += 6;
    }
    else if (!IsSolidRenderingBlock(ReadPackedBlockBuffer(chunkData, currPos - flatFactor.x)))
    {
        faceNeg.x = true;
        vCount += 6;
    }

    if (id.y <= 0)
    {
        faceNeg.y = true;
        vCount += 6;
    }
    else if (!IsSolidRenderingBlock(ReadPackedBlockBuffer(chunkData, currPos - flatFactor.y)))
    {
        faceNeg.y = true;
        vCount += 6;
    }

    if (id.z <= 0)
    {
        faceNeg.z = true;
        vCount += 6;
    }
    else if (!IsSolidRenderingBlock(ReadPackedBlockBuffer(chunkData, currPos - flatFactor.z)))
    {
        faceNeg.z = true;
        vCount += 6;
    }

    if (id.x >= 31)
    {
        facePos.x = true;
        vCount += 6;
    }
    else if (!IsSolidRenderingBlock(ReadPackedBlockBuffer(chunkData, currPos + flatFactor.x)))
    {
        facePos.x = true;
        vCount += 6;
    }

    if (id.y >= 31)
    {
        facePos.y = true;
        vCount += 6;
    }
    else if (!IsSolidRenderingBlock(ReadPackedBlockBuffer(chunkData, currPos + flatFactor.y)))
    {
        facePos.y = true;
        vCount += 6;
    }

    if (id.z >= 31)
    {
        facePos.z = true;
        vCount += 6;
    }
    else if (!IsSolidRenderingBlock(ReadPackedBlockBuffer(chunkData, currPos + flatFactor.z)))
    {
        facePos.z = true;
        vCount += 6;
    }

    InterlockedAdd(indirectBuffer[0], vCount, currIdx);
}

[numthreads(8, 8, 8)]
void GetVertCount(uint3 id : SV_DispatchThreadID)
{
    int currPos = id.x * flatFactor.x + id.y * flatFactor.y + id.z * flatFactor.z;
    if (IsRenderableBlock(ReadPackedBlockBuffer(chunkData, currPos)))
    {
        bool3 facePos, faceNeg;
        int i;
        SetVert(id, currPos, i, facePos, faceNeg);
    }
}

[numthreads(8, 8, 8)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    int currPos = id.x * flatFactor.x + id.y * flatFactor.y + id.z * flatFactor.z;
    uint fsData = fsPointerBuffer[currPos];
    if (IsRenderableBlock(ReadPackedBlockBuffer(chunkData, currPos)))
    {
        bool3 facePos, faceNeg;
        int i;
        SetVert(id, currPos, i, facePos, faceNeg);

        float4 blockSize = float4(1.0, 1.0, 1.0, 0);
        float3 blockOffset = float3(-0.05, -0.05, -0.05) + float3(id.x, id.y, id.z);

        Vertex v;

        v.data = ReadPackedBlockBuffer(chunkData, currPos);
        bool isFS = ((fsData & 0x80000000) > 0);

        float2 uv0 = float2(0, 0);
        float2 uv1 = float2(0, 1);
        float2 uv2 = float2(1, 0);
        float2 uv3 = float2(1, 1);

        if (isFS)
        {
            uint ix = fsData & 0x7fffffff;
            float2 uv = float2(float(ix / fsBufLenY) / float(fsBufLenX), float(ix % fsBufLenY) / float(fsBufLenY));
            uv0 = uv;
            uv1 = uv;
            uv2 = uv;
            uv3 = uv;
        }

        // Left
        if (faceNeg.x)
        {
            v.normal = float3(-1, 0, 0);

            v.position = blockOffset;
            v.block_vert_meta = 0x0;
            v.uv = uv0;
            vertexBuffer[i] = v;

            v.position = blockOffset + blockSize.wwz;
            v.block_vert_meta = 0x1;
            v.uv = uv1;
            vertexBuffer[i + 1] = v;

            v.position = blockOffset + blockSize.wyw;
            v.block_vert_meta = 0x2;
            v.uv = uv2;
            vertexBuffer[i + 2] = v;

            v.position = blockOffset + blockSize.wyw;
            v.block_vert_meta = 0x2;
            v.uv = uv2;
            vertexBuffer[i + 3] = v;

            v.position = blockOffset + blockSize.wwz;
            v.block_vert_meta = 0x1;
            v.uv = uv1;
            vertexBuffer[i + 4] = v;

            v.position = blockOffset + blockSize.wyz;
            v.block_vert_meta = 0x3;
            v.uv = uv3;
            vertexBuffer[i + 5] = v;

            i += 6;
        }

        // Bottom
        if (faceNeg.y)
        {
            v.normal = float3(-1, 0, 0);

            v.position = blockOffset;
            v.block_vert_meta = 0x0;
            v.uv = uv0;
            vertexBuffer[i] = v;

            v.position = blockOffset + blockSize.xww;
            v.block_vert_meta = 0x4;
            v.uv = uv1;
            vertexBuffer[i + 1] = v;

            v.position = blockOffset + blockSize.wwz;
            v.block_vert_meta = 0x1;
            v.uv = uv2;
            vertexBuffer[i + 2] = v;

            v.position = blockOffset + blockSize.wwz;
            v.block_vert_meta = 0x1;
            v.uv = uv2;
            vertexBuffer[i + 3] = v;

            v.position = blockOffset + blockSize.xww;
            v.block_vert_meta = 0x4;
            v.uv = uv1;
            vertexBuffer[i + 4] = v;

            v.position = blockOffset + blockSize.xwz;
            v.block_vert_meta = 0x5;
            v.uv = uv3;
            vertexBuffer[i + 5] = v;

            i += 6;
        }

        // Back
        if (faceNeg.z)
        {
            v.normal = float3(0, 0, -1);

            v.position = blockOffset;
            v.block_vert_meta = 0x0;
            v.uv = uv0;
            vertexBuffer[i] = v;

            v.position = blockOffset + blockSize.wyw;
            v.block_vert_meta = 0x2;
            v.uv = uv1;
            vertexBuffer[i + 1] = v;

            v.position = blockOffset + blockSize.xww;
            v.block_vert_meta = 0x4;
            v.uv = uv2;
            vertexBuffer[i + 2] = v;

            v.position = blockOffset + blockSize.xww;
            v.block_vert_meta = 0x4;
            v.uv = uv2;
            vertexBuffer[i + 3] = v;

            v.position = blockOffset + blockSize.wyw;
            v.block_vert_meta = 0x2;
            v.uv = uv1;
            vertexBuffer[i + 4] = v;

            v.position = blockOffset + blockSize.xyw;
            v.block_vert_meta = 0x6;
            v.uv = uv3;
            vertexBuffer[i + 5] = v;

            i += 6;
        }

        // Right
        if (facePos.x)
        {
            v.normal = float3(1, 0, 0);

            v.position = blockOffset + blockSize.xww;
            v.block_vert_meta = 0x4;
            v.uv = uv0;
            vertexBuffer[i] = v;

            v.position = blockOffset + blockSize.xyw;
            v.block_vert_meta = 0x6;
            v.uv = uv1;
            vertexBuffer[i + 1] = v;

            v.position = blockOffset + blockSize.xwz;
            v.block_vert_meta = 0x5;
            v.uv = uv2;
            vertexBuffer[i + 2] = v;

            v.position = blockOffset + blockSize.xwz;
            v.block_vert_meta = 0x5;
            v.uv = uv2;
            vertexBuffer[i + 3] = v;

            v.position = blockOffset + blockSize.xyw;
            v.block_vert_meta = 0x6;
            v.uv = uv1;
            vertexBuffer[i + 4] = v;

            v.position = blockOffset + blockSize.xyz;
            v.block_vert_meta = 0x7;
            v.uv = uv3;
            vertexBuffer[i + 5] = v;

            i += 6;
        }

        // Top
        if (facePos.y)
        {
            v.normal = float3(0, 1, 0);

            v.position = blockOffset + blockSize.wyw;
            v.block_vert_meta = 0x2;
            v.uv = uv0;
            vertexBuffer[i] = v;

            v.position = blockOffset + blockSize.wyz;
            v.block_vert_meta = 0x3;
            v.uv = uv1;
            vertexBuffer[i + 1] = v;

            v.position = blockOffset + blockSize.xyw;
            v.block_vert_meta = 0x6;
            v.uv = uv2;
            vertexBuffer[i + 2] = v;

            v.position = blockOffset + blockSize.xyw;
            v.block_vert_meta = 0x6;
            v.uv = uv2;
            vertexBuffer[i + 3] = v;

            v.position = blockOffset + blockSize.wyz;
            v.block_vert_meta = 0x3;
            v.uv = uv1;
            vertexBuffer[i + 4] = v;

            v.position = blockOffset + blockSize.xyz;
            v.block_vert_meta = 0x7;
            v.uv = uv3;
            vertexBuffer[i + 5] = v;

            i += 6;
        }

        // Forward
        if (facePos.z)
        {
            v.normal = float3(0, 0, 1);

            v.position = blockOffset + blockSize.wwz;
            v.block_vert_meta = 0x1;
            v.uv = uv0;
            vertexBuffer[i] = v;

            v.position = blockOffset + blockSize.xwz;
            v.block_vert_meta = 0x5;
            v.uv = uv1;
            vertexBuffer[i + 1] = v;

            v.position = blockOffset + blockSize.wyz;
            v.block_vert_meta = 0x3;
            v.uv = uv2;
            vertexBuffer[i + 2] = v;

            v.position = blockOffset + blockSize.wyz;
            v.block_vert_meta = 0x3;
            v.uv = uv2;
            vertexBuffer[i + 3] = v;

            v.position = blockOffset + blockSize.xwz;
            v.block_vert_meta = 0x5;
            v.uv = uv1;
            vertexBuffer[i + 4] = v;

            v.position = blockOffset + blockSize.xyz;
            v.block_vert_meta = 0x7;
            v.uv = uv3;
            vertexBuffer[i + 5] = v;

            i += 6;
        }
    }
}
